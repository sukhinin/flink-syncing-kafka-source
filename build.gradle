plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.6.1'
}

group 'com.github.sukhinin'
description 'Synchronizing Kafka source for Apache Flink'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    implementation 'org.apache.flink:flink-connector-kafka_2.12:1.10.0'
    compileOnly 'org.apache.flink:flink-streaming-java_2.12:1.10.0'
}

java {
    withSourcesJar()
}

jgitver {
    useDirty true
    useGitCommitID true
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = project.description
                url = 'https://github.com/sukhinin/flink-syncing-kafka-source'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://github.com/sukhinin/flink-syncing-kafka-source/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'sukhinin'
                        name = 'Boris Sukhinin'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/sukhinin/flink-syncing-kafka-source.git'
                    developerConnection = 'scm:git:https://github.com/sukhinin/flink-syncing-kafka-source.git'
                    url = 'https://github.com/sukhinin/flink-syncing-kafka-source'
                }
            }
        }
    }
}

bintray {
    user = project.findProperty('bintray.user') ?: System.getenv('BINTRAY_USER')
    key = project.findProperty('bintray.apikey') ?: System.getenv('BINTRAY_APIKEY')
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/sukhinin/flink-syncing-kafka-source'
        issueTrackerUrl = 'https://github.com/sukhinin/flink-syncing-kafka-source/issues'
        vcsUrl = 'https://github.com/sukhinin/flink-syncing-kafka-source.git'
        licenses = ['Apache-2.0']
        labels = ['flink', 'kafka', 'watermark', 'timestamp', 'sync']
        githubRepo = 'sukhinin/flink-syncing-kafka-source'
    }
}
